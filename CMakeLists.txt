set(PROJ_SRC_DIR ${CMAKE_CURRENT_SOURCE_DIR}/src)
set(SRCS_C "")
set(SRCS_CPP "")
set(SRCS_COMPILE_OPTIONS "-DLV_LVGL_H_INCLUDE_SIMPLE")
set(INCLUDE_DIRS ${PROJ_SRC_DIR})
set(EMBED_FILES "")

#
# GUI
#
if(CONFIG_ESP_BROOKESIA_CONF_ENABLE_GUI)
    list(APPEND SRCS_COMPILE_OPTIONS "-DESP_BROOKESIA_CONF_ENABLE_GUI=1")
    set(GUI_SRC_DIR ${PROJ_SRC_DIR}/gui)
    file(GLOB GUI_SRCS_CPP ${GUI_SRC_DIR}/*.cpp)
    list(APPEND SRCS_CPP ${GUI_SRCS_CPP})
    # Squareline
    set(GUI_SQUARELINE_SRC_DIR ${GUI_SRC_DIR}/squareline)
    if(CONFIG_ESP_BROOKESIA_CONF_GUI_SQUARELINE_ENABLE_UI_COMP)
        # UI Components
        set(GUI_SQUARELINE_UI_COMP_SRC_DIR ${GUI_SQUARELINE_SRC_DIR}/ui_comp)
        file(GLOB_RECURSE GUI_SQUARELINE_UI_COMP_SRCS_C ${GUI_SQUARELINE_UI_COMP_SRC_DIR}/*.c)
        list(APPEND SRCS_C ${GUI_SQUARELINE_UI_COMP_SRCS_C})
    endif()
    if(CONFIG_ESP_BROOKESIA_CONF_GUI_SQUARELINE_ENABLE_UI_HELPERS)
        # UI Helpers
        set(GUI_SQUARELINE_UI_HELPERS_SRC_DIR ${GUI_SQUARELINE_SRC_DIR}/ui_helpers)
        file(GLOB_RECURSE GUI_SQUARELINE_UI_HELPERS_SRCS_C ${GUI_SQUARELINE_UI_HELPERS_SRC_DIR}/*.c)
        list(APPEND SRCS_C ${GUI_SQUARELINE_UI_HELPERS_SRCS_C})
    endif()
    # LVGL
    set(GUI_LVGL_SRC_DIR ${GUI_SRC_DIR}/lvgl)
    file(GLOB_RECURSE GUI_LVGL_SRCS_C ${GUI_LVGL_SRC_DIR}/*.c)
    file(GLOB_RECURSE GUI_LVGL_SRCS_CPP ${GUI_LVGL_SRC_DIR}/*.cpp)
    list(APPEND SRCS_C ${GUI_LVGL_SRCS_C})
    list(APPEND SRCS_CPP ${GUI_LVGL_SRCS_CPP})
endif()

#
# Systems
#
if(CONFIG_ESP_BROOKESIA_CONF_ENABLE_SYSTEMS)
    list(APPEND SRCS_COMPILE_OPTIONS "-DESP_BROOKESIA_CONF_ENABLE_SYSTEMS=1")
    set(SYSTEM_SRC_DIR ${PROJ_SRC_DIR}/systems)
    # Assets
    set(SYSTEM_ASSETS_SRC_DIR ${SYSTEM_SRC_DIR}/assets)
    file(GLOB_RECURSE SYSTEM_ASSETS_SRC_C ${SYSTEM_ASSETS_SRC_DIR}/*.c)
    list(APPEND SRCS_C ${SYSTEM_ASSETS_SRC_C})
    # Core
    set(SYSTEM_CORE_SRC_DIR ${SYSTEM_SRC_DIR}/core)
    file(GLOB_RECURSE SYSTEM_CORE_SRCS_C ${SYSTEM_CORE_SRC_DIR}/*.c)
    file(GLOB_RECURSE SYSTEM_CORE_SRCS_CPP ${SYSTEM_CORE_SRC_DIR}/*.cpp)
    list(APPEND SRCS_C ${SYSTEM_CORE_SRCS_C})
    list(APPEND SRCS_CPP ${SYSTEM_CORE_SRCS_CPP})
    # Phone
    if(CONFIG_ESP_BROOKESIA_CONF_SYSTEMS_ENABLE_PHONE)
        list(APPEND SRCS_COMPILE_OPTIONS "-DESP_BROOKESIA_CONF_SYSTEMS_ENABLE_PHONE=1")
        set(SYSTEM_PHONE_SRC_DIR ${SYSTEM_SRC_DIR}/phone)
        file(GLOB SYSTEM_PHONE_SRCS_C ${SYSTEM_PHONE_SRC_DIR}/*.c)
        file(GLOB SYSTEM_PHONE_SRCS_CPP ${SYSTEM_PHONE_SRC_DIR}/*.cpp)
        list(APPEND SRCS_C ${SYSTEM_PHONE_SRCS_C})
        list(APPEND SRCS_CPP ${SYSTEM_PHONE_SRCS_CPP})
        # App Examples
        if(CONFIG_ESP_BROOKESIA_CONF_PHONE_ENABLE_APP_EXAMPLES)
            list(APPEND SRCS_COMPILE_OPTIONS "-DESP_BROOKESIA_CONF_PHONE_ENABLE_APP_EXAMPLES=1")
            if(CONFIG_ESP_BROOKESIA_CONF_PHONE_APP_EXAMPLES_ENABLE_SIMPLE_CONF)
                list(APPEND SRCS_COMPILE_OPTIONS "-DESP_BROOKESIA_CONF_PHONE_APP_EXAMPLES_ENABLE_SIMPLE_CONF=1")
                file(GLOB_RECURSE SYSTEM_PHONE_APP_EXAMPLES_SRCS_C ${SYSTEM_PHONE_SRC_DIR}/app_examples/simple_conf/*.c)
                file(GLOB_RECURSE SYSTEM_PHONE_APP_EXAMPLES_SRCS_CPP ${SYSTEM_PHONE_SRC_DIR}/app_examples/simple_conf/*.cpp)
                list(APPEND SRCS_C ${SYSTEM_PHONE_APP_EXAMPLES_SRCS_C})
                list(APPEND SRCS_CPP ${SYSTEM_PHONE_APP_EXAMPLES_SRCS_CPP})
            endif()
            if(CONFIG_ESP_BROOKESIA_CONF_PHONE_APP_EXAMPLES_ENABLE_COMPLEX_CONF)
                list(APPEND SRCS_COMPILE_OPTIONS "-DESP_BROOKESIA_CONF_PHONE_APP_EXAMPLES_ENABLE_COMPLEX_CONF=1")
                file(GLOB_RECURSE SYSTEM_PHONE_APP_EXAMPLES_SRCS_C ${SYSTEM_PHONE_SRC_DIR}/app_examples/complex_conf/*.c)
                file(GLOB_RECURSE SYSTEM_PHONE_APP_EXAMPLES_SRCS_CPP ${SYSTEM_PHONE_SRC_DIR}/app_examples/complex_conf/*.cpp)
                list(APPEND SRCS_C ${SYSTEM_PHONE_APP_EXAMPLES_SRCS_C})
                list(APPEND SRCS_CPP ${SYSTEM_PHONE_APP_EXAMPLES_SRCS_CPP})
            endif()
            if(CONFIG_ESP_BROOKESIA_CONF_PHONE_APP_EXAMPLES_ENABLE_SQUARELINE)
                list(APPEND SRCS_COMPILE_OPTIONS "-DESP_BROOKESIA_CONF_PHONE_APP_EXAMPLES_ENABLE_SQUARELINE=1")
                file(GLOB_RECURSE SYSTEM_PHONE_APP_EXAMPLES_SRCS_C ${SYSTEM_PHONE_SRC_DIR}/app_examples/squareline/*.c)
                file(GLOB_RECURSE SYSTEM_PHONE_APP_EXAMPLES_SRCS_CPP ${SYSTEM_PHONE_SRC_DIR}/app_examples/squareline/*.cpp)
                list(APPEND SRCS_C ${SYSTEM_PHONE_APP_EXAMPLES_SRCS_C})
                list(APPEND SRCS_CPP ${SYSTEM_PHONE_APP_EXAMPLES_SRCS_CPP})
            endif()
        endif()
        # Assets
        file(GLOB_RECURSE SYSTEM_PHONE_ASSETS_SRCS_C ${SYSTEM_PHONE_SRC_DIR}/assets/*.c)
        file(GLOB_RECURSE SYSTEM_PHONE_ASSETS_SRCS_CPP ${SYSTEM_PHONE_SRC_DIR}/assets/*.cpp)
        list(APPEND SRCS_C ${SYSTEM_PHONE_ASSETS_SRCS_C})
        list(APPEND SRCS_CPP ${SYSTEM_PHONE_ASSETS_SRCS_CPP})
        # Widgets
        file(GLOB_RECURSE SYSTEM_PHONE_WIDGETS_SRCS_C ${SYSTEM_PHONE_SRC_DIR}/widgets/*.c)
        file(GLOB_RECURSE SYSTEM_PHONE_WIDGETS_SRCS_CPP ${SYSTEM_PHONE_SRC_DIR}/widgets/*.cpp)
        list(APPEND SRCS_C ${SYSTEM_PHONE_WIDGETS_SRCS_C})
        list(APPEND SRCS_CPP ${SYSTEM_PHONE_WIDGETS_SRCS_CPP})
    endif()
endif()

if(ESP_PLATFORM)
    idf_component_register(
        SRCS ${SRCS_C} ${SRCS_CPP}
        INCLUDE_DIRS ${INCLUDE_DIRS}
        EMBED_FILES ${EMBED_FILES}
    )
else()
    set(COMPONENT_LIB esp-brookesia)
    add_library(${COMPONENT_LIB} STATIC ${SRCS_C} ${SRCS_CPP})
    target_link_libraries(${COMPONENT_LIB} lvgl esp-lib-utils)
    target_include_directories(${COMPONENT_LIB} PUBLIC ${PROJ_SRC_DIR})
endif()

target_compile_options(${COMPONENT_LIB}
    PUBLIC
        ${SRCS_COMPILE_OPTIONS}
    PRIVATE
        $<$<COMPILE_LANGUAGE:CXX>:-std=gnu++20>
)
if(NOT ESP_PLATFORM)
    target_compile_definitions(${COMPONENT_LIB} PUBLIC ESP_BROOKESIA_KCONFIG_IGNORE)
endif()

set(SRCS_C_COMPILE_FLAGS "-Wno-format")
set(SRCS_CPP_COMPILE_FLAGS "-Wno-missing-field-initializers -Wno-format")
set_source_files_properties(
    ${SRCS_CPP}
    PROPERTIES
        COMPILE_FLAGS "${SRCS_CPP_COMPILE_FLAGS}"
)
set_source_files_properties(
    ${SRCS_C}
    PROPERTIES
        COMPILE_FLAGS "${SRCS_C_COMPILE_FLAGS}"
)
set_source_files_properties(
    ${SYSTEM_PHONE_APP_EXAMPLES_SRCS_C}
    PROPERTIES
        COMPILE_FLAGS "${SRCS_C_COMPILE_FLAGS} -Wno-unused-variable"
)
